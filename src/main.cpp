#include <Arduino.h>
#include <SparkFunMLX90614.h>
#include <U8g2lib.h>
#ifdef U8X8_HAVE_HW_SPI
#include <SPI.h>
#endif
#ifdef U8X8_HAVE_HW_I2C
#include <Wire.h>
#endif
#include "PubSubClient.h"
#include "WiFi.h"
#include <aliyun_mqtt.h>
#include <ArduinoJson.h>
#include "buzzer.h"
#include "main.h"

//! setup
void setup()
{
	xTaskCreate(setupTask, "setup task", 1024 * 4, NULL, 1, NULL);
}

//! loop
void loop() {}

// ğŸ‘‹
void setupTask(void *ptParams)
{
	initDisplayFun("systems initialize...");
	Serial.begin(115200); // è®¾ç½®ä¸²å£æ³¢ç‰¹ç‡
	pinSetup();
	u8g2.begin();
	therm.begin();
	therm.setUnit(TEMP_C);
	Wire.begin(); // åˆå§‹åŒ–ä¸ºI2Cä¸»æœº SHTC3

	weightInit = readHX711(); // è·å–å¼€æœºæ—¶çš„é‡åŠ›ä¼ æ„Ÿå™¨æ•°æ®
	// æ‰“å°åˆå§‹åŒ–çš„ é‡é‡å€¼ï¼šweightInit
	Serial.printf("weightInit Date: %ld\n", weightInit);
	Serial.flush();

	initDisplayFun("Network initialize...");
	WifiSetup();
	if (connectAliyunMQTT(mqttClient, PRODUCT_KEY, DEVICE_NAME, DEVICE_SECRET))
	{
		Serial.println("MQTTæœåŠ¡å™¨è¿æ¥æˆåŠŸ!");
	}
	mqttClient.subscribe(ALINK_TOPIC_PROP_SET); // ! è®¢é˜…Topic !!è¿™æ˜¯å…³é”®!!
	mqttClient.setCallback(mqttCallback);		// ç»‘å®šæ”¶åˆ°setä¸»é¢˜æ—¶çš„å›è°ƒ(å‘½ä»¤ä¸‹å‘1å›è°ƒ)

	initDisplayFun("system initialize...");
	// ä»»åŠ¡åˆå§‹åŒ–: éƒ½åœ¨ `core1` ä¸Šåˆ›å»ºä»»åŠ¡
	xMutexData = xSemaphoreCreateMutex(); // åˆ›å»ºMutex
	if (xMutexData == NULL)
	{
		Serial.println("No Enough RAM, unable to create `Semaphore.`");
	}
	xMutexBuzzer = xSemaphoreCreateMutex(); // åˆ›å»º buzzer mutex
	if (xMutexBuzzer == NULL)
	{
		Serial.println("No Enough RAM, unable to create `Semaphore.`");
	}

	if (xTaskCreatePinnedToCore(sensorGetTask, "sensorGetTask", 1024 * 5, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("sensorGetTask åˆ›å»ºæˆåŠŸ");
	if (xTaskCreatePinnedToCore(sensorLogTask, "sensorLogTask", 1024 * 5, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("sensorLogTask åˆ›å»ºæˆåŠŸ");
	if (xTaskCreatePinnedToCore(displayTask, "displayTask", 1024 * 5, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("displayTask åˆ›å»ºæˆåŠŸ");
	if (xTaskCreatePinnedToCore(mqttCheck, "mqttCheck", 1024 * 5, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("mqttCheck åˆ›å»ºæˆåŠŸ");
	if (xTaskCreatePinnedToCore(controllerTask, "controllerTask", 1024 * 5, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("controllerTask åˆ›å»ºæˆåŠŸ");

	if (xTaskCreatePinnedToCore(sendMsgTask, "sendMsgTask", 1024 * 5, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("sendMsgTask åˆ›å»ºæˆåŠŸ");

	if (xTaskCreatePinnedToCore(buzzerTask, "buzzer music", 1024 * 4, NULL, 1, NULL, 1) == pdPASS)
		Serial.println("buzzerLoop åˆ›å»ºæˆåŠŸ");

	// BUG[å®šæ—¶å™¨ä¸­æœ‰`sprintf`æ“ä½œå°±ä¼šå¯¼è‡´é‡å¯ï¼Œä½¿ç”¨taskæ›¿æ¢è¯¥åŠŸèƒ½çš„å®ç°] åˆ›å»ºå®šæ—¶å™¨
	// sendMsgTimerHandle = xTimerCreate("sendMsg timer", 2000, pdTRUE, (void *)1, sendMsgTimerCallback);
	// xTimerStart(sendMsgTimerHandle, portMAX_DELAY);

	initDisplayFun("initialize over!");
	vTaskDelay(pdMS_TO_TICKS(1000));
	vTaskDelete(NULL);
}

void sensorGetTask(void *ptParams)
{
	const TickType_t xFrequency = 100; // é‡‡é›†é¢‘ç‡ 100 ticks = 100ms
	TickType_t lastSleepTick = xTaskGetTickCount();
	while (true)
	{
		// æ‹¿å–é’¥åŒ™
		if (xSemaphoreTake(xMutexData, timeout) == pdPASS)
		{
			readSHTC3();
			getWeight();
			readRainDrop();
			readMLX();
			// LOG
			// Serial.println("get sensor data.");
			// é‡Šæ”¾é”
			xSemaphoreGive(xMutexData);
		}
		vTaskDelayUntil(&lastSleepTick, xFrequency);
	}
}

// ä¼ æ„Ÿå™¨æ•°æ®æ‰“å°task
void sensorLogTask(void *ptParams)
{
	const TickType_t xFrequency = 1000;
	TickType_t lastSleepTick = xTaskGetTickCount();
	while (true)
	{
		if (xSemaphoreTake(xMutexData, timeout) == pdPASS)
		{
			Serial.printf("BodyTemperature: %.2fÂ°\n", data.bodyTemperature);
			Serial.printf("Humidity: %.2f%%, Temperature: %.2f\n", data.envTemperature, data.envHumidity);
			Serial.printf("Weight: %ldg\n", data.weightTrue);
			Serial.printf("RainDrop: %d\n", data.rainDrop);
			// é‡Šæ”¾é”
			xSemaphoreGive(xMutexData);
		}
		vTaskDelayUntil(&lastSleepTick, xFrequency);
	}
}

// OLEDæ˜¾ç¤ºtask
void displayTask(void *ptParams)
{
	while (true)
	{
		u8g2.clearBuffer();
		u8g2.setFont(u8g2_font_ncenB08_tr);
		u8g2.setColorIndex(2);
		char info1[32];
		char info2[32];
		char info3[32];
		char info4[32];
		char info5[32];
		if (xSemaphoreTake(xMutexData, timeout) == pdPASS)
		{
			sprintf(info1, "Weight: %ldg", data.weightTrue);
			sprintf(info2, "EnvTemp: %.2f", data.envTemperature);
			sprintf(info3, "EnvHum: %.2f%%", data.envHumidity);
			sprintf(info4, "BodyTemp: %.2f", data.bodyTemperature);
			sprintf(info5, "RainDrop: %d", data.rainDrop);

			xSemaphoreGive(xMutexData);
		}
		else
		{
			continue;
		}
		u8g2.drawStr(0, 10, info1);
		u8g2.drawStr(0, 20, info2);
		u8g2.drawStr(0, 30, info3);
		u8g2.drawStr(0, 40, info4);
		u8g2.drawStr(0, 50, info5);
		u8g2.sendBuffer();
	}
}

// mqttè¿æ¥æ£€æŸ¥taskï¼Œ1sæ£€æŸ¥ä¸€æ¬¡
void mqttCheck(void *ptParams)
{
	const TickType_t xFrequency = 1000; // 1000 ticks = 1000ms
	TickType_t xLastSleepTick = xTaskGetTickCount();
	while (true)
	{
		mqttCheck();	   // æ£€æŸ¥mqttè¿æ¥
		mqttClient.loop(); // mqttå®¢æˆ·ç«¯ç›‘å¬ï¼Œä¸ä¼šé˜»å¡
		vTaskDelayUntil(&xLastSleepTick, xFrequency);
	}
}

// ç¡¬ä»¶æ§åˆ¶ [æŠ¥è­¦,åŠ æ¹¿å™¨,å°¿æ¹¿éŸ³ä¹,ç”µçƒ­æ¯¯,å¥¶ç“¶åŠ çƒ­]
void controllerTask(void *ptParams)
{
	while (true)
	{
		if (xSemaphoreTake(xMutexData, timeout) == pdPASS)
		{
			buzzerHandler();		  // èœ‚é¸£å™¨
			humidifierHandler();	  // åŠ æ¹¿å™¨
			electricBlanketHandler(); // ç”µçƒ­æ¯¯
			bottleHeatingHandler();	  // å¥¶ç“¶åŠ çƒ­
			xSemaphoreGive(xMutexData);
		}
	}
}

void sendMsgTask(void *ptParams)
{
	const TickType_t xFrequency = 5000;
	TickType_t lastSleepTick = xTaskGetTickCount();
	while (true)
	{
		if (mqttClient.connected())
		{
			// å…ˆæ‹¼æ¥å‡ºjsonå­—ç¬¦ä¸²
			char param[128] = {0};
			char jsonBuf[256] = {0};
			if (xSemaphoreTake(xMutexData, timeout) == pdPASS)
			{
				// æŠŠè¦ä¸Šä¼ çš„æ•°æ®å†™åœ¨paramä¸­
				sprintf(param,
						"{\"BodyTemperature\":%.2f,\"EnvTemperature\":%.2f,\"EnvHumidity\":%.2f,\"RainDrop\":%d}",
						data.bodyTemperature, data.envTemperature, data.envHumidity, data.rainDrop);

				postMsgId += 1;
				sprintf(jsonBuf, ALINK_BODY_FORMAT, postMsgId, ALINK_METHOD_PROP_POST, param);
				xSemaphoreGive(xMutexData);

				// å†ä»mqttå®¢æˆ·ç«¯ä¸­å‘å¸ƒpostæ¶ˆæ¯
				if (mqttClient.publish(ALINK_TOPIC_PROP_POST, jsonBuf))
				{
					Serial.print("Post message to cloud: ");
					Serial.println(jsonBuf);
					// å‘é€æˆåŠŸæ¿è½½LEDé—ªçƒ
					digitalWrite(LED_PIN, HIGH);
					vTaskDelay(pdMS_TO_TICKS(50)); // 50ms
					digitalWrite(LED_PIN, LOW);
				}
				else
				{
					Serial.println("Publish message to cloud failed!");
				}
			}
		}
		vTaskDelayUntil(&lastSleepTick, xFrequency);
	}
}

// FIXED ã€ä¿®å¤ã€‘å¤šçŠ¶æ€èœ‚é¸£å™¨æ§åˆ¶
void buzzerTask(void *ptParams)
{
	int channel = 0;	// é€šé“
	int freq = 2;		// é¢‘ç‡
	int resolution = 8; // åˆ†è¾¨ç‡
	ledcSetup(channel, freq, resolution);
	ledcAttachPin(BUZZER_PIN, channel);

	int length = sizeof(tune) / sizeof(tune[0]); // è®¡ç®—é•¿åº¦
	int xMode = 0;

	while (true)
	{
		if (xSemaphoreTake(xMutexBuzzer, timeout) == pdPASS)
		{
			xMode = buzzerMode.mode;
			xSemaphoreGive(xMutexBuzzer);
		}
		switch (xMode)
		{
		case 0:
			// Serial.println("buzzer: none");  // log
			ledcWrite(channel, 0);
			break;
		case 1:
			// Serial.println("buzzer: alert"); // log
			ledcWrite(channel, 255);
			vTaskDelay(pdMS_TO_TICKS(1000));
			break;
		case 2:
			// Serial.println("buzzer: music");  // log
			for (int x = 0; x < length; x++)
			{
				if (xSemaphoreTake(xMutexBuzzer, timeout) == pdPASS)
				{
					xMode = buzzerMode.mode;
					xSemaphoreGive(xMutexBuzzer);
					if (xMode != 2)
						break;
				}
				ledcWriteTone(0, tune[x]);
				vTaskDelay(500 * durt[x]); // è¿™é‡Œç”¨æ¥æ ¹æ®èŠ‚æ‹è°ƒèŠ‚å»¶æ—¶ï¼Œ500è¿™ä¸ªæŒ‡æ•°å¯ä»¥è‡ªå·±è°ƒæ•´ï¼Œåœ¨è¯¥éŸ³ä¹ä¸­ï¼Œæˆ‘å‘ç°ç”¨500æ¯”è¾ƒåˆé€‚ã€‚
			}
			// ledcWriteTone(0, 0);
			vTaskDelay(pdMS_TO_TICKS(500));
			break;
		default:
			break;
		}
	}
}

// åˆå§‹åŒ–GPIO
void pinSetup()
{
	pinMode(HX711_SCK_PIN, OUTPUT); // HX711é‡åŠ›ä¼ æ„Ÿå™¨ GPIOåˆå§‹åŒ–
	pinMode(HX711_DT_PIN, INPUT);
	pinMode(BUZZER_PIN, OUTPUT);  // æŠ¥è­¦GPIOåˆå§‹åŒ–
	pinMode(LED_PIN, OUTPUT);	  // æ¿è½½LEDåˆå§‹åŒ–
	pinMode(RAINDROP_PIN, INPUT); // é›¨æ»´ä¼ æ„Ÿå™¨GPIOåˆå§‹åŒ–
	pinMode(HUMIDIFIER_PIN, OUTPUT);
	pinMode(ELECTRIC_BLANKET_PIN, OUTPUT);
	pinMode(BOTTLE_HEAT_PIN, OUTPUT);
}

/**
 * @brief wifiè¿æ¥
 */
void WifiSetup()
{
	delay(10);
	Serial.println("è¿æ¥WIFI");
	WiFi.begin(WIFI_SSID, WIFI_PASSWD);
	while (!WiFi.isConnected())
	{
		Serial.print(".");
		// vTaskDelay(pdMS_TO_TICKS(500));  // 500ms
		delay(500); // 500ms
	}
	Serial.println("OK");
	Serial.println("Wifiè¿æ¥æˆåŠŸ");
}

/**
 * @brief æ”¶åˆ°æ¶ˆæ¯å›è°ƒ
 * @param topic è®¢é˜…çš„topic
 * @param payload æ¥å—çš„æ¶ˆæ¯è´Ÿè½½
 * @param length æ¶ˆæ¯é•¿åº¦
 */
void mqttCallback(char *topic, byte *payload, unsigned int length)
{
	if (strstr(topic, ALINK_TOPIC_PROP_SET))
	{
		Serial.println("æ”¶åˆ°ä¸‹å‘çš„å‘½ä»¤ä¸»é¢˜:");
		Serial.println(topic);
		Serial.println("ä¸‹å‘çš„å†…å®¹æ˜¯:");
		payload[length] = '\0'; // ä¸ºpayloadæ·»åŠ ä¸€ä¸ªç»“æŸé™„,é˜²æ­¢Serial.println()è¯»è¿‡äº†
		Serial.println((char *)payload);

		// æ¥ä¸‹æ¥æ˜¯æ”¶åˆ°çš„jsonå­—ç¬¦ä¸²çš„è§£æ
		DynamicJsonDocument doc(150);
		DeserializationError error = deserializeJson(doc, payload);
		if (error)
		{
			Serial.println("parse json failed");
			Serial.println(error.c_str());
			return;
		}
		JsonObject setAlinkMsgObj = doc.as<JsonObject>();
		serializeJsonPretty(setAlinkMsgObj, Serial);
		Serial.println();

		// å›è°ƒæ•°æ®å¤„ç†
		aliData.bottleHeatStatus = setAlinkMsgObj["params"]["bottleHeat"];		// { "name": "bottleHeat", "type": "bool" }
		aliData.alertStatus = setAlinkMsgObj["params"]["alert"];				// { "name": "alert", "type": "bool" }
		aliData.electricBlankStatus = setAlinkMsgObj["params"]["electriBlank"]; // { "name": "electriBlank", "type": "bool" }
	}
}

/**
 * @brief mqttå®¢æˆ·ç«¯é‡è¿å‡½æ•°, å¦‚æœå®¢æˆ·ç«¯æ–­çº¿,å¯ä»¥é€šè¿‡æ­¤å‡½æ•°é‡è¿
 */
void clientReconnect()
{
	while (!mqttClient.connected())
	{ // å†é‡è¿å®¢æˆ·ç«¯
		Serial.println("reconnect MQTT...");
		if (connectAliyunMQTT(mqttClient, PRODUCT_KEY, DEVICE_NAME, DEVICE_SECRET))
		{
			Serial.println("connected");
		}
		else
		{
			Serial.println("failed");
			Serial.println(mqttClient.state());
			Serial.println("try again in 5 sec");
			delay(5000);
		}
	}
}

/**
 * @brief MQTTç½‘ç»œæ£€æµ‹
 */
void mqttCheck()
{
	if (!WiFi.isConnected())
	{ // åˆ¤æ–­WiFiæ˜¯å¦è¿æ¥
		WifiSetup();
	}
	else
	{ // å¦‚æœWIFIè¿æ¥äº†,
		if (!mqttClient.connected())
		{ // å†æ¬¡åˆ¤æ–­mqttæ˜¯å¦è¿æ¥æˆåŠŸ
			Serial.println("mqtt disconnected!Try reconnect now...");
			Serial.println(mqttClient.state());
			clientReconnect();
		}
	}
}

/**
 * @brief è¯»å–mlxæ¸©åº¦
 * @version 2.0.0
 * @date 2022.6.9
 * @author lxy@qq.com
 */
void readMLX()
{
	if (therm.read())
	{ // On success, read() will return 1, on fail 0.
		String s = String(therm.object(), 2);
		data.bodyTemperature = atoi(s.c_str());
	}
}

/**
 * @brief SHTC3çš„CRCæ ¡éªŒ
 * @param DAT
 * @param CRC_DAT
 * @return uint8_t
 */
uint8_t SHTC3_CRC_CHECK(uint16_t DAT, uint8_t CRC_DAT)
{
	uint8_t i, t, temp;
	uint8_t CRC_BYTE;
	CRC_BYTE = 0xFF;
	temp = (DAT >> 8) & 0xFF;
	for (t = 0; t < 2; t++)
	{
		CRC_BYTE ^= temp;
		for (i = 0; i < 8; i++)
		{
			if (CRC_BYTE & 0x80)
			{
				CRC_BYTE <<= 1;
				CRC_BYTE ^= 0x31;
			}
			else
			{
				CRC_BYTE <<= 1;
			}
		}
		if (t == 0)
		{
			temp = DAT & 0xFF;
		}
	}
	if (CRC_BYTE == CRC_DAT)
	{
		temp = 1;
	}
	else
	{
		temp = 0;
	}
	return temp;
}

/**
 * @brief è·å–æ¸©æ¹¿åº¦æ•°æ®
 */
void readSHTC3()
{
	Wire.beginTransmission(SHTC3_ADDRESS); // æ ¹æ®åœ°å€0x70ï¼Œå¼€å§‹å‘I2Cçš„ä»æœºè¿›è¡Œä¼ è¾“ã€‚
	Wire.write(byte(0xE0));				   // å‘é€å†™å…¥æŒ‡ä»¤
	Wire.endTransmission();				   // åœæ­¢å‘ä»æœºä¼ è¾“
	Wire.beginTransmission(SHTC3_ADDRESS);
	Wire.write(byte(0x35)); // å‘é€å”¤é†’æŒ‡ä»¤çš„é«˜ä½éƒ¨åˆ†
	Wire.write(byte(0x17)); // å‘é€å”¤é†’æŒ‡ä»¤çš„ä½ä½éƒ¨åˆ†
	Wire.endTransmission();
	delayMicroseconds(300); // å»¶æ—¶300å¾®ç§’
	Wire.beginTransmission(SHTC3_ADDRESS);
	Wire.write(byte(0xE0));
	Wire.endTransmission();
	Wire.beginTransmission(SHTC3_ADDRESS);
	Wire.write(byte(0x7C)); // å‘é€é‡‡é›†æŒ‡ä»¤çš„é«˜ä½éƒ¨åˆ†
	Wire.write(byte(0xA2)); // å‘é€é‡‡é›†æŒ‡ä»¤çš„ä½ä½éƒ¨åˆ†
	Wire.endTransmission();
	Wire.beginTransmission(SHTC3_ADDRESS);
	Wire.write(byte(0xE1)); // å‘é€è¯»å–æŒ‡ä»¤
	Wire.endTransmission();
	Wire.requestFrom(SHTC3_ADDRESS, 6); // å‘ä»æœºè¯·æ±‚æ•°æ®
	uint16_t T_temp, RH_temp, T_CRC, RH_CRC;
	if (2 <= Wire.available())
	{
		T_temp = Wire.read();  // æ¥æ”¶æ¸©åº¦é«˜ä½æ•°æ®
		T_temp = T_temp << 8;  // å·¦ç§»8ä½
		T_temp |= Wire.read(); // å·¦ç§»8ä½åçš„æ¸©åº¦é«˜ä½æ•°æ®ä¸æ¥æ”¶åˆ°çš„æ¸©åº¦ä½ä½æ•°æ®è¿›è¡ŒæŒ‰ä½æˆ–è¿ç®—
		T_CRC = Wire.read();   // æ¥æ”¶CRCæ ¡éªŒç 
		if (SHTC3_CRC_CHECK(T_temp, T_CRC))
		{															// æ ¡éªŒæ•°æ®
			data.envTemperature = float(T_temp) * 175 / 65536 - 45; // è®¡ç®—å‡ºæ¸©åº¦
		}
	}
	if (2 <= Wire.available())
	{
		RH_temp = Wire.read();	// æ¥æ”¶æ¹¿åº¦é«˜ä½æ•°æ®
		RH_temp = RH_temp << 8; // å·¦ç§»8ä½
		RH_temp |= Wire.read(); // å·¦ç§»8ä½åçš„æ¹¿åº¦é«˜ä½æ•°æ®ä¸æ¥æ”¶åˆ°çš„æ¹¿åº¦ä½ä½æ•°æ®è¿›è¡ŒæŒ‰ä½æˆ–è¿ç®—
		RH_CRC = Wire.read();
		if (SHTC3_CRC_CHECK(RH_temp, RH_CRC))
		{
			data.envHumidity = float(RH_temp) * 100 / 65536;
		}
	}
}

/**
 * @brief è¯»å–é‡åŠ›ä¼ æ„Ÿå™¨æ•°æ® => é€‰æ‹©èŠ¯ç‰‡å·¥ä½œæ–¹å¼å¹¶è¿›è¡Œæ•°æ®è¯»å–
 * @author lxy
 * @return é‡åŠ›ä¼ æ„Ÿå™¨åŸå§‹æ•°æ®
 */
unsigned long readHX711(void)
{
	unsigned long count = 0; // å‚¨å­˜è¾“å‡ºå€¼
	unsigned char i;

	digitalWrite(HX711_DT_PIN, HIGH);
	delayMicroseconds(1); // å»¶æ—¶ 1å¾®ç§’
	digitalWrite(HX711_SCK_PIN, LOW);
	delayMicroseconds(1); // å»¶æ—¶ 1å¾®ç§’

	// å½“DTçš„å€¼ä¸º1æ—¶ï¼Œå¼€å§‹adè½¬æ¢
	while (digitalRead(HX711_DT_PIN))
		;
	// 24ä¸ªè„‰å†²ï¼Œå¯¹åº”è¯»å–24ä½æ•°å€¼
	for (i = 0; i < 24; i++)
	{
		// åˆ©ç”¨ SCKä»0--1 ï¼Œå‘é€ä¸€æ¬¡è„‰å†²ï¼Œè¯»å–æ•°å€¼
		digitalWrite(HX711_SCK_PIN, HIGH);

		delayMicroseconds(1);			  // å»¶æ—¶ 1å¾®ç§’
		count = count << 1;				  // ç”¨äºç§»ä½å­˜å‚¨24ä½äºŒè¿›åˆ¶æ•°å€¼
		digitalWrite(HX711_SCK_PIN, LOW); // ä¸ºä¸‹æ¬¡è„‰å†²åšå‡†å¤‡
		delayMicroseconds(1);
		if (digitalRead(HX711_DT_PIN)) // è‹¥DTå€¼ä¸º1ï¼Œå¯¹åº”countè¾“å‡ºå€¼ä¹Ÿä¸º1
			count++;
	}
	digitalWrite(HX711_SCK_PIN, HIGH); // å†æ¥ä¸€æ¬¡ä¸Šå‡æ²¿ é€‰æ‹©å·¥ä½œæ–¹å¼  128å¢ç›Š

	// æŒ‰ä½å¼‚æˆ–,ä¸åŒåˆ™ä¸º1 => 0^0=0; 1^0=1;
	// å¯¹åº”äºŒè¿›åˆ¶ => [1000 0000 0000 0000 0000 0000]; ä½œç”¨ä¸ºå°†æœ€é«˜ä½å–å,å…¶ä»–ä½ä¿ç•™åŸå€¼
	count ^= 0x800000;
	delayMicroseconds(1);

	digitalWrite(HX711_SCK_PIN, LOW); // SCK=0ï¼›
	delayMicroseconds(1);
	// è¿”å›ä¼ æ„Ÿå™¨è¯»å–å€¼
	return count;
}

/**
 * @brief è·å–ç‰©ä½“çœŸå®é‡é‡
 * @author lxy
 */
void getWeight()
{
	long HX711_Buffer;
	HX711_Buffer = readHX711();									  // è¯»å–ä¼ æ„Ÿå™¨è¾“å‡ºå€¼
	data.weightTrue = HX711_Buffer;								  // å°†ä¼ æ„Ÿå™¨çš„è¾“å‡ºå€¼å‚¨å­˜
	data.weightTrue -= weightInit;								  // è·å–å®ç‰©çš„ADé‡‡æ ·æ•°å€¼ã€‚
	data.weightTrue = (long)((float)data.weightTrue / GAP_VALUE); // ADå€¼è½¬æ¢ä¸ºé‡é‡ï¼ˆgï¼‰
																  // Serial.printf("data weight: %d", data.weightTrue);
}

/**
 * @brief è¯»å–é›¨æ»´ä¼ æ„Ÿå™¨æ•°æ®
 */
void readRainDrop(void)
{
	data.rainDrop = digitalRead(RAINDROP_PIN);
}

// åŠ æ¹¿å™¨handler
void humidifierHandler(void)
{
	if (data.envHumidity < HUMIDIFIER_THRESHOLD_VALUE)
	{
		digitalWrite(HUMIDIFIER_PIN, HIGH);
	}
	else
	{
		digitalWrite(HUMIDIFIER_PIN, LOW);
	}
}

/**
 * @brief æŠ¥è­¦handler
 */
void buzzerHandler(void)
{
	if (xSemaphoreTake(xMutexBuzzer, timeout) == pdPASS)
	{
		// æŠ¥è­¦: åœ¨å¾®ä¿¡å°ç¨‹åºæŠ¥è­¦æ˜¯å…³çš„çŠ¶æ€ä¸‹ï¼Œé‡é‡ä½äºé¢„è®¾å€¼
		if (data.weightTrue < WEIGHT_THRESHOLD_VALUE && !aliData.alertStatus)
		{
			buzzerMode.mode = 1;
			xSemaphoreGive(xMutexBuzzer);
			return;
		}
		else if (data.rainDrop)
		{
			// buzzer music
			buzzerMode.mode = 2;
			xSemaphoreGive(xMutexBuzzer);
			return;
		}
		else
		{
			buzzerMode.mode = 0;
			xSemaphoreGive(xMutexBuzzer);
			return;
		}
	}
}

/**
 * @brief ç”µçƒ­æ¯¯åŠ çƒ­handler
 */
void electricBlanketHandler(void)
{
	// @deprecated æ ¹æ®ç¯å¢ƒæ¸©åº¦ï¼ŒESP32è‡ªåŠ¨æ§åˆ¶ç”µçƒ­æ¯¯
	// if (data.envTemperature < ELECTRON_BLANKET_THRESHOLD_VALUE)
	// {
	// 	digitalWrite(ELECTRIC_BLANKET_PIN, HIGH);
	// }
	// else
	// {
	// 	digitalWrite(ELECTRIC_BLANKET_PIN, LOW);
	// }

	// å¾®ä¿¡å°ç¨‹åºæ§åˆ¶ç”µçƒ­æ¯¯
	if (aliData.electricBlankStatus)
	{
		digitalWrite(ELECTRIC_BLANKET_PIN, HIGH);
	}
	else
	{
		digitalWrite(ELECTRIC_BLANKET_PIN, LOW);
	}
}

/**
 * @brief [å¥¶ç“¶] ç”±å¾®ä¿¡å°ç¨‹åºæ§åˆ¶å¼€å¯æˆ–ç»™å…³é—­å¥¶ç“¶åŠ çƒ­
 */
void bottleHeatingHandler(void)
{
	if (aliData.bottleHeatStatus)
	{
		digitalWrite(BOTTLE_HEAT_PIN, HIGH);
	}
	else
	{
		digitalWrite(BOTTLE_HEAT_PIN, LOW);
	}
}

// åˆå§‹åŒ–æ˜¾ç¤ºä¿¡æ¯
void initDisplayFun(const char *info)
{
	u8g2.clearBuffer();
	u8g2.setFont(u8g2_font_ncenB08_tr);
	u8g2.setColorIndex(2);
	char buffer[32];
	sprintf(buffer, "%s", info);
	u8g2.drawStr(0, 10, buffer);
	u8g2.sendBuffer();
}